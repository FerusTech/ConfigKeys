import java.text.SimpleDateFormat

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }

    dependencies {
        classpath "gradle.plugin.ninja.miserable:blossom:1.0.1"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.0"
        classpath "net.nemerosa:versioning:2.0.0"
    }
}

apply plugin: "net.nemerosa.versioning"

final Date buildDateTime = new Date()
ext {
    buildBy = System.properties['user.name']
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildDateTime)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildDateTime)
    buildCreatedBy = "${System.properties["java.version"]} (${System.properties["java.vendor"]} ${System.properties["java.vm.version"]})".toString()

    final String[] split = gitBranch.split("/")
    split[0] = split[0].toUpperCase()

    gitBranchStripped = split.join("-")

    gitHash = versioning.info.commit
    gitHashShort = versioning.info.build
}

group = "tech.ferus"
version = "5.0.0" + (gitBranch == "master" ? "" : gitBranch == "develop" ? "-SNAPSHOT" : "-" + gitBranchStripped + "-SNAPSHOT")
description = "ConfigKeys is a simple wrapper for zml's Configurate, providing a more stream-lined way to access configuration on-the-fly."

apply plugin: "java"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "ninja.miserable.blossom"

apply from: file("gradle/artifacts.gradle")
apply from: file("gradle/publish.gradle")

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.code.findbugs:jsr305:3.0.2"
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "ninja.leaping.configurate:configurate-core:3.3"
    compile "ninja.leaping.configurate:configurate-gson:3.3"
    compile "ninja.leaping.configurate:configurate-hocon:3.3"
    compile "ninja.leaping.configurate:configurate-json:3.3"
    compile "ninja.leaping.configurate:configurate-yaml:3.3"
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.5"
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}